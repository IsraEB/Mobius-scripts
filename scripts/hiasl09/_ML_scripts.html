<html>
<head>
<title></title>
</head>

<body>

these are some scripts I use to control Möbius.<br><br>


Of course they are adapted to my style of playing which is mainly multitrack looping, muting and unmuting of tracks. I use as Nobels MF1 foot controller which can only send program changes so some of the scripts are written to have more than one function on one button.<br>

I hope some ideas are useful for other loopers to achieve similar functionality or improve it and hopefully tell me how.<br><br>



My settings are: Quantize=off, switch Quant = off, Mute Mode = start, Loop copy = off, track copy = timing.<br>
<br>



<b>Record, Retrigger</b><br>
<br>


I discovered that I hardly use the record button in a track that already contains a loop. So this script records to an empty track (if mode = reset), otherwise it does a GlobalRetrigger to all unmuted tracks.

<pre>
#################################
!name RecordRetrigger

Jump 1stLoop mode = Reset                     #uses the old Jump-statement instead of an if-loop
Jump 1stLoop mode = Record

Variable Muted
Variable saveSwitchQuant
For all
 set saveSwitchQuant $switchQuant
 set switchQuant off
 set Muted $mode
 Jump MuteAgain Muted = Mute
 Retrigger
 Jump goon

 Label MuteAgain
 Retrigger
 Mute

 Label goon
 set switchQuant $saveSwitchQuant
next
end

Label 1stLoop
Record
Record up
set Input 127

Variable track changeMax
set changeMax 0
end

#################################
</pre>
<br>
<br>
<b>Audio Recording</b><br>
<br>
This puts the function of Audio Recording to one button, the Custom Mode gives you visual feedback.<br>
1st press starts recording, second press stops and saves recording.
<br>
<pre>
#################################
!AudioRec

Variable global Recording

if Recording = 1
StopAudioRecording
SaveAudioRecording
set Recording 0
set CustomMode "Recording saved"
wait msecs 888
set CustomMode
else
StartAudioRecording
set Recording 1

set CustomMode "Recording started"
wait msecs 666
set CustomMode
endif
#################################
</pre><br>
<br>
<b>Loop recording</b><br>
<br>
simple, just for visual feedback.<br>
<br>
<pre>
#################################
!LoopSave

if mode != reset
SaveLoop
set CustomMode "loop saved"
wait msecs 555
set CustomMode
endif
#################################
</pre>

<b>Reset</b> <br>
<br>


I use Reset (loop) General reset (all loops in track) and Global Reset (for all loops in all tracks).<br>
The reset script also resets the trackcontrols. It also cancels bouncing (not really, see: Overdub, Bounce)<br>
If you bind this to a key or a button a single-click resets the loop (and volume, feedback, rate etc.) a double click resets all loops and selects track 1. very handy!
<br>
<br>



<pre>
#################################
!DoubleReset

Variable global resetAll

if resetAll = 1
    For all
    GeneralReset
    Next

    For all
    set feedback 127               #deactivate by # in the beginning of the line or remove line if you don't use this
    set output 127
    set pan 64
    set group 0
    PitchShift 0
    RateShift 0
    Next

    Variable global BounceNow
    set BounceNow 0
    set CustomMode

    TrackSelect1                      #selects track one after double click
    set resetAll 0

else

    Reset
    set feedback 127
    set output 127
    set pan 64
    set group 0

    Variable global BounceNow
    set BounceNow 0
    set CustomMode

    PitchShift 0
    RateShift 0

    set resetAll 1
    set CustomMode "Global Reset?"
    wait msecs 250                        #adjust double click speed here
    set resetAll 0
    set CustomMode

endif
#################################
</pre>
<br>
<br>
<b>Overdub, Bounce</b>
<br>
<br>
This Overdub script acts as Overdub (of course), if the track you want to overdub is muted, it retriggers all playing (unmuted) tracks, including the selected, so you can start overdubbing from the beginning.<br>
<br>
I also use the overdub button to Bounce, because I rarely overdub something while bouncing to a new track.<br>
If you are in an empty track (mode = reset) möbius Mode will show you: "bounce?"  you now can cancel this with the above reset script or start bouncing with the overdub button, which retriggers all playing tracks at the same time, then it selects the previous track (where one can start to fool around with all sorts of functions) and ends bouncing by another press of the overdub button.

<pre>
#################################
!name OverdubNow

Variable global BounceNow
Jump Bouncing BounceNow > 0

if mode = reset
  set BounceNow 1
  set CustomMode "bounce?"
endif
################

set Input 127

Jump dubNow mode = Mute

Variable SaveSwitchQuant = $switchQuant
set switchQuant off
Overdub
set switchQuant $SaveSwitchQuant
end

Label dubNow

Variable Muted
Variable saveSwitchQuant
For all
 set saveSwitchQuant $switchQuant
 set switchQuant off
 set Muted $mode
 Jump MuteAgain Muted = Mute
 Retrigger
 Jump goon

 Label MuteAgain
 Retrigger
 Mute

 Label goon
 set switchQuant $saveSwitchQuant
next

Overdub
end
#################
Label Bouncing

if BounceNow = 2
 Bounce
 set BounceNow 0
 set CustomMode
elseif BounceNow = 1
For all
 Variable saveSwitchQuant
 set saveSwitchQuant $switchQuant
 set switchQuant off
 Jump MuteAgain2 mode = Mute
 Retrigger
 Jump goon2

 Label MuteAgain2
 Retrigger
 Mute

 Label goon2
 set switchQuant $saveSwitchQuant
next
 PrevTrack
 Bounce
 set BounceNow 2
 set CustomMode bouncing
endif

end
#################################
</pre>

<b>Next Loop</b><br>
<br>
I tried to cover a lot of Next Loop appearances by the use of two buttons.<br>
<br>
<b>quantized</b><br>
<br>

the first button (script) does a quantized Next Loop. There is a double click behaviour: if you double click the button the timing will be copied to the next loop, otherwise not. If the track is in reset mode, this button resets all loops in the current track. Once resetted the button will change the loop Count (moreLoops) parameter (see remarks in script). Therefore it's good to have it displayed.<br>
(General reset will only work if "set changeMax 0" is executed before, so I have put this line also in other scripts, like record, reset, unqu. nextLoop etc.)<br>
<br>
there is an additional function: If you schedule a quantized NextLoop and go to another track and do a unquantized NextLoop (see script below) it will follow this command to do an unquantized next loop immediately.<br>
<br>



<pre>
#################################
!name QuantNextLoop

Jump Reset_LCount mode = Reset

Variable track SaveCopyMode
set SaveCopyMode $loopCopy
Variable track SaveSwitchQuant
set SaveSwitchQuant $switchQuant
Variable track switching
Variable track CopyTiming
Variable track changeMax

set switching on
set switchQuant loop

if CopyTiming = yes
    set switchQuant $SaveSwitchQuant
    set LoopCopy timing
else

    NextLoop
    set changeMax 0
    set switchQuant $SaveSwitchQuant
    set CopyTiming yes
    set CustomMode
endif

wait msecs 234                                      #double click time
set CopyTiming no
set CustomMode

Wait start
set loopCopy $SaveCopyMode
set switching off

end
#################
Label Reset_LCount

Variable track changeMax
if changeMax = 1
    For all
    Variable maxloops
    set maxloops 3                # set number to maximum Number of Loops you want to use
                                  # (not higher than in the preset dialog!)

    if moreLoops = maxloops
        set moreLoops 2
    elseif moreLoops = 2
        set moreLoops 3
    elseif moreLoops = 3
        set moreLoops 4
    elseif moreLoops = 4
        set moreLoops 5
    elseif moreLoops = 5
        set moreLoops 6
    elseif moreLoops = 6
        set moreLoops 7
    elseif moreLoops = 7
        set moreLoops 8
    elseif moreLoops = 8
        set moreLoops 2
    endif
    next
else

GeneralReset

set feedback 127
set output 127
set pan 64
set group 0
PitchShift 0
RateShift 0

set customMode "Track Reset"
Wait msecs 333
set customMode


set changeMax 1
endif

end
#################################
</pre>

<b>Unquantized</b>                                                                                 <br>
<br>


this script does NextLoop unquantized (immediately) and also retriggers (unmutes) all other loops, if another track is in  "switching" mode (quantized NextLoop, by the above script) it will be switched to Next Loop simoultanously.
<br>
<br>
(nice to have some percussive things in one track beeing muted and retriggered at the beginning of a new phrase or having more tracks going to NextLoop at the same time, even if they have different lengths.


<pre>
#################################
!name NextLoopNow

Variable track selected
set selected on
Variable track changeMax

For all
Variable track switching

if selected = on
    if switching = on
        Undo
        set switchQuant off
    endif

    NextLoop
    set selected off
else
    if switching = on
        Undo
        set switchQuant off
        NextLoop
    else
        Retrigger
    endif
endif

For All
set switching off
next

set changeMax 0
next
end
#################################
</pre>

<br><br>

<b>Next Track, previous Track</b>
<br>
<br>
I use the next and previous track buttons for two more functions. If I am in overdub, previous track will perform "undo", next track will perform "multiply".

<pre>
#################################
!prevTrackUndo
Jump second mode = Overdub
PrevTrack
end
Label second
PLay
Undo
end
#################################
</pre>        <br>
<br>

<pre>
#################################
!NextTrackMultiply
Jump Mply mode = Overdub
Jump Mply mode = Multiply
NextTrack
end
Label Mply
Multiply
end
#################################
</pre>

<br>
<br>
<b>unqu. retrigger, toggle input</b><br>
<br>
simple, but I use it very often: this retriggers the current track, but when in record or in overdub mode it toggles the input between 127 and zero.

<pre>
#################################
Jump onoff mode = Record
Jump onoff mode = Overdub

Variable SaveSwitchQuant = $switchQuant
set switchQuant off
Retrigger
set switchQuant SaveSwitchQuant
end

Label onoff
Jump onagain Input = 0
set Input 0
end
Label onagain
set Input 127
end
#################################
</pre>




</body>
</html>
<!-- neodl2.grp.bf1.yahoo.com Wed May  2 18:18:50 UTC 2018 -->
